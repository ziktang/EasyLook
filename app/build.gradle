apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.example.tctctc.easylook"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "v1.0"

        // dex突破65535的限制
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            // No debug config
        }

        release {
            storeFile file("EsayReadkey.jks")
            storePassword "esayread"
            keyAlias "EsayRead"
            keyPassword "esayread"
        }
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "LOG_ON", "true")//通过编译类型配置日志开关
            versionNameSuffix "-debug"                       //包名后缀为“-debug”
            minifyEnabled false                              //是否混淆
            zipAlignEnabled false                            //Zipalign优化
            shrinkResources false                            // 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug               //使用debug证书签名
        }

        release {
            buildConfigField "boolean", "LOG_ON", "false" //不显示Log
            zipAlignEnabled true                             //开启Zipalign优化
            shrinkResources true                             //移除无用的resource文件，此项只有在开启混淆时才生效
            minifyEnabled true
            signingConfig signingConfigs.release             //使用release证书签名
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //多渠道打包配置
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为test_v1.0_2016-10-9_wandoujia.apk
                        def fileName = "EsayRead_${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}-${variant.buildType.name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

// 渠道配置
    productFlavors {
        xiaomi {}
        wandoujia {}
        _360 {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name] //动态地修改AndroidManifest中的渠道名
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.zhy:okhttputils:2.6.2'
    //recycleview定制框架
    compile 'com.jude:easyrecyclerview:4.2.0'

    compile 'com.android.support:design:23.4.0'
    testCompile 'junit:junit:4.12'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.android.support:cardview-v7:23.4.0'
    //图片加载框架
    compile 'com.github.bumptech.glide:glide:3.7.0'

    //rxjava,retrofit,rxbinding
    compile 'io.reactivex:rxjava:x.y.z'
    compile 'io.reactivex:rxandroid:+'

    compile 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'

    compile 'com.umeng.analytics:analytics:latest.integration'

    dependencies {
        debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
        releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
        testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    }
}
